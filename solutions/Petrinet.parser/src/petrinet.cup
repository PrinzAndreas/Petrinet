package parser;

//old - should be removed
import java.util.ArrayList;
//new
import src.StructureCreation;
import org.jetbrains.mps.openapi.model.SNode;
import java_cup.runtime.*;

/** Parser for Petri nets connected to MPS. */

// Keywords
terminal PETRINET;   // "petrinet"
terminal PLACE;      // "place"
terminal TRANSITION; // "transition"

// Identifiers and Numbers
terminal String IDENTIFIER; // name
terminal Integer NUMBER;

// Separators
terminal OPEN_PAR;    // (
terminal CLOSE_PAR;   // )
terminal OPEN_BRACE;  // {
terminal CLOSE_BRACE; // }
terminal COLON;       // :
terminal COMMA;       // ,
terminal ARROW;       // =>

// non-terminals
non terminal java.util.ArrayList petrinet;
non terminal java.util.ArrayList item_list;
non terminal java.util.ArrayList item;
non terminal java.util.ArrayList place;
non terminal java.util.ArrayList transition;
non terminal String place_ref;
non terminal java.util.ArrayList ref_list;

// Grammar
start with petrinet;

// Rules
petrinet ::= PETRINET IDENTIFIER:n OPEN_BRACE item_list:l CLOSE_BRACE 
      {: ArrayList<Object> res = new ArrayList(); res.add("Petrinet"); res.add(n); res.add(l); RESULT = res; :}
    ;

item_list ::= item_list:l item:i
      {: ArrayList<Object> res = new ArrayList(l); res.add(i); RESULT = res; :}
    | item:i
      {: ArrayList<Object> res = new ArrayList(); res.add(i); RESULT = res; :}
    ;

item ::= place:p
      {: RESULT = p; :}
    | transition:t 
      {: RESULT = t; :}
    ;

place ::= PLACE IDENTIFIER:n OPEN_PAR NUMBER:i CLOSE_PAR
      {: SNode sn = StructureCreation.createPlace(n,i); ArrayList<Object> res = new ArrayList(); res.add("Place"); res.add(n); res.add(i); RESULT = res; :}
    ;

transition ::= TRANSITION IDENTIFIER:n COLON ref_list:in ARROW ref_list:out
      {: ArrayList<Object> res = new ArrayList(); res.add("Transition"); res.add(n); res.add(in); res.add(out); RESULT = res; :}
    ;

ref_list ::= ref_list:l COMMA place_ref:r
      {: ArrayList<Object> res = new ArrayList(l); res.add(r); RESULT = res; :}
    | place_ref:r
      {: ArrayList<Object> res = new ArrayList(); res.add(r); RESULT = res; :}
    ;

place_ref ::= IDENTIFIER:n {: RESULT = n; :}
    ;